def parse-type [] {
  let value = $in
  {
    name:         $value.name?
    type:         $value.type?.names?.0?
    description:  $value.description?
    optional:     $value.optional?
  }
}

# Parses json generated by jsdoc to generate a new doctable.
#
# `in`: should be a `jsdoc` generated JSON document
#
# Example: `doc use (npx jsdoc -X <path to source directory>|doc src:javascript parse-from-jsdoc)`
def parse-from-jsdoc [] {
  from json
  |where undocumented? != true and kind? != 'package'
  |each {|row|
    let isConstructor = $row.kind? == 'class' and $row.params? != null
    {
      name: (if ($isConstructor) { $"new ($row.longname?)" } else { $row.longname? }),
      summary: ($row.description?|default ""|split row "."|get 0?),
      signatures: (if ($row.params? != null) {
        [(
          $row.params
          |each { parse-type }
          |append (if ($row.returns?.0? != null) {
            $row.returns.0|parse-type
          } else if ($isConstructor) {
            [{ name: null, type: $row.name?, description: null }]
          } else {
            []
          })
        )]
      })
      description: $row.description?
      kind: (
        if ($isConstructor) {
          'constructor'
        } else {
          $row.kind?
        }
      )
      source: $row.comment?
      examples: (
        $row.examples?
        |default []
        |each {$"```javascript\n($in)\n```"}
      )
      ns: $row.memberof?
    }
  }
}

# Parses doctable using `jsdoc -X`. Output can be unpredictable
# for projects not using jsdoc syntax. You can run
# `less $"($env.PKD_HOME)/doc/jsdoc-run"`
# for more details on how jsdoc is invoked.
#
# ### Examples:
# ```nushell
# # Mount jsdocs for ramda.js
# > doc src:jsdoc use node_modules/ramda/src/
# ```
export def --env use [
  filepath:string # path to the directory jsdoc should be run against
] {
  let absolutePath = $filepath|path expand
  let generatorCommand = $"src:jsdoc use ($absolutePath|to nuon)"
  do --env $env.DOC_USE {{
    about: {
      name: $filepath
      text_format: 'markdown'
      generator: 'src:jsdoc'
      generator_command: $generatorCommand
    }
    doctable: (run-external $"($env.PKD_HOME)/doc/jsdoc-run" $absolutePath|parse-from-jsdoc)
  }} $generatorCommand
}